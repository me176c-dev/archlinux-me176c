# Maintainer: lambdadroid <lambdadroid@gmail.com>
pkgname=me176c-android-boot
pkgver=0.1.0
pkgrel=1
pkgdesc="Android bootloader for the ASUS MeMO Pad 7 (ME176C/X)"
arch=(x86_64)
url="https://github.com/me176c-dev/me176c-boot"
license=('LGPL3')
depends=('me176c-systemd-boot')
makedepends=('meson' 'gnu-efi-libs' 'libpng')
backup=(
    'boot/loader/loader.conf'
    'boot/loader/entries/android.conf'
    'boot/loader/entries/fastboot.conf'
    'boot/loader/entries/recovery.conf'
)

_efiver=0.1.0
source=(
    "me176c-boot-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
    "android-efi-$pkgver.tar.gz::https://github.com/me176c-dev/android-efi/archive/$_efiver.tar.gz"
)
sha256sums=('d8a72537ec847e76c1904ae0cb5a35a98d5bf236143863f527b5a3fb37feb769'
            '285714f58305172703fbf3b609998dfe236aebaae70c285776632426128f804f')

build() {
    meson "android-efi-$_efiver" build -Drsci=true
    ninja -C build
}

package() {
    # Bootloader
    install -Dm644 build/android.efi "$pkgdir/boot/android.efi"

    # Default configuration files
    cd "me176c-boot-$pkgver"
    install -Dm644 loader/loader.conf "$pkgdir/boot/loader/loader.conf"
    install -Dm644 loader/entries/android.conf "$pkgdir/boot/loader/entries/android.conf"
    install -Dm644 loader/entries/fastboot.conf "$pkgdir/boot/loader/entries/fastboot.conf"
    install -Dm644 loader/entries/recovery.conf "$pkgdir/boot/loader/entries/recovery.conf"
}
