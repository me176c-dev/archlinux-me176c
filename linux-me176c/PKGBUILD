# $Id$
# Maintainer: lambdadroid <lambdadroid@gmail.com>

#pkgbase=linux               # Build stock -ARCH kernel
pkgbase=linux-me176c         # Build kernel with a different name
_srcname=linux-4.14
pkgver=4.14.40
pkgrel=1
arch=('x86_64')
url="https://github.com/me176c-dev/me176c-archlinux"
license=('GPL2')
makedepends=('xmlto' 'kmod' 'inetutils' 'bc' 'libelf' 'iasl')
options=('!strip')
source=(
  "https://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.xz"
  "https://www.kernel.org/pub/linux/kernel/v4.x/${_srcname}.tar.sign"
  "https://www.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz"
  "https://www.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.sign"
  'config'         # the main kernel config file
  '60-linux.hook'  # pacman hook for depmod
  '90-linux.hook'  # pacman hook for initramfs regeneration
  'linux.preset'   # standard config files for mkinitcpio ramdisk
  'git+https://github.com/me176c-dev/me176c-acpi.git#commit=03640c055a488dc27f9860fca64a6a1c10bc2bc9'
  '0001-UPSTREAM-usb-dwc3-core-Fix-ULPI-PHYs-and-prevent-phy.patch'
  '0002-ME176C-drivers-Makefile-Move-mfd-before-gpu.patch'
  '0003-ME176C-ASoC-Intel-Atom-Force-use-of-BYT-CR-BIOS-reso.patch'
  '0004-ASoC-Intel-bytcr_rt5640-Add-quirks-for-ASUS-MeMO-Pad.patch'
  '0005-iio-accel-kxcjk-1013-Add-the-KXJ2109-ACPI-id.patch'
  '0006-iio-magnetometer-ak8975-Add-the-AKM9911-ACPI-id.patch'
  '0007-ME176C-x86-Add-support-to-optimize-kernel-for-Silver.patch'
  '0008-ME176C-input-misc-Add-ASUS-Lid-Cover-driver.patch'
  '0009-ME176C-ACPI-tables-Remap-GNVS-opregion-memory-offset.patch'
  '0010-ME176C-ASoC-Intel-bytcr_rt5640_jack-Headphone-detect.patch'
  '0011-ME176C-pinctrl-baytrail-Remove-warnings-caused-by-ha.patch'
  '0012-media-staging-atomisp-Avoid-using-trace_printk-by-de.patch'
  '0013-ME176C-Revert-pinctrl-baytrail-Do-not-add-all-GPIOs-.patch'
  '0014-ME176C-Revert-pinctrl-intel-set-default-handler-to-b.patch'
  '0015-ME176C-Revert-gpio-acpi-Override-GPIO-initialization.patch'
  '0016-ME176C-PM-Sleep-Add-wake-lock-api-wrapper-on-top-of-.patch'
  '0017-ME176C-intel-mid-Add-compatibility-layer-for-old-int.patch'
  '0018-ME176C-power-supply-Import-battery-charger-drivers.patch'
  '0019-ME176C-power-supply-Fix-battery-charger-drivers-v2.patch'
  '0020-ME176C-extcon-Add-driver-for-Intel-Baytrail-Crystal-.patch'
  '0021-ME176C-phy-tusb1210-Implement-basic-charger-detectio.patch'
  '0022-ME176C-pwm-crc-Turn-off-backlight-when-shutting-down.patch'
  '0023-ME176C-drm-i915-Avoid-changing-brightness-to-max-aft.patch'
  '0024-ME176C-drm-i915-Make-maximum-PWM-brightness-configur.patch'
  '0025-ME176C-Revert-Input-goodix-poll-the-buffer-status-bi.patch'
)
validpgpkeys=(
  'ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linus Torvalds
  '647F28654894E3BD457199BE38DBBDC86092693E'  # Greg Kroah-Hartman
)
sha256sums=('f81d59477e90a130857ce18dc02f4fbe5725854911db1e7ba770c7cd350f96a7'
            'SKIP'
            '128ee971b97421aea56ddf093d860586054419b166a7a21a55a4389bd4dbbb2f'
            'SKIP'
            '2929bf51848b478d63ed3b1e0d6e5b9798d330b25ea579ac03ad59877546aaaa'
            'ae2e95db94ef7176207c690224169594d49445e04249d2499e9d2fbc117a0b21'
            '75f99f5239e03238f88d1a834c50043ec32b1dc568f2cc291b07d04718483919'
            'ad6344badc91ad0630caacde83f7f9b97276f80d26a20619a87952be65492c65'
            'SKIP'
            'b387048374df3ea46056762febaf51c8f0784f594fdcbc6c4d3235cc30760b2d'
            '69e84cff23a7155e8b47b4bb04cd7e590bbc708a3b404b1b8475b2296efefb4b'
            'c3ec4d49126140d36f9915c22b7e7dbdf68a8eb823d2500d2589850f2da276f0'
            '32f38f55142b9e3e1784cd7849db147d5479fb51d13a5553e553095de798bbe0'
            '87104f5688806178eff0be43ec09c7a374a47653400d45c06d052ed20590cbfd'
            'cd031f91765689eb62048cc1cdc7c6f8dcd7e73163aa733154d974388108e508'
            '1e912ac1a1931ebd993a2b3c49885ae5beaa35f3d48e27b0c7836f56bc463021'
            '058ce3a92ea601bc3105c8478dba5cf7d2263fd606f801a0174472973dbd12f4'
            'adcec94d90cccb6c1aeb79523ec01c23851b8e48e0a114fe3c7aa55b3a7e2f85'
            'af13b439947c42e0cdcb2c6804796dc7bac6a740fe2216aa42b7013a2c2ac576'
            'c74f71a8f7a152e99575cf6772c907a15050e98eb9efd9cf236bd3e5928479b7'
            'c9596848e44d35c8feaa7d9bde4d30a0a09f0615bde0fef4315f1a5597bd5af9'
            'd6569a242411f44e2eca987ebfec8b03db1237356adbfa64ee91f34b88ae6376'
            '6572bc579a08cb5ec75b8797080642e087d3c52f934d378daaeb00da391e30ee'
            '4beffe5c9c931fde6ad593cf29bf23072c345716efaa7e6d423b3b567fba5cee'
            '43e75a8e69508d0d475ba6353ab39190a3a3cf2c1a3f70be80abaa858ba89c11'
            '51ee828da606d94772091d60c997f64874d7e054d41d160b0f4154cca2633eb8'
            '1dac9e06a1a66f4b75ef1d4a7142dcc48c1ce61158e9a0f9fbf629111fc50341'
            'c5eb5cb52b355b343d1ef89188e9c350d853d053c1932d1c1cbe04664896e3e9'
            '0a826e227bec8ab399078a3327a6510c2e142628de657190da1c13883f722762'
            '0468a39b67102a8af55a16036e862ae82820f0fe9697e5c65a68b7c6d65f4501'
            '8f9850324ee48b0d7ca9395290ba7197511e3783b683943fefec47f5509242f3'
            'a7e79d929a9583b8198bc9aa570c1d99d8382c77f8e5f10f520531f40419da05'
            '0eefff0e771db0e915f3f13a90a3bb239c12364b0475aca2add89f8a4ba53726'
            '2f57ed9605916dbfae665fbdcac82d868253c9f70a81e12fdeb11e8abc56fea6')

_kernelname=${pkgbase#linux}

prepare() {
  # Compile ACPI DSDT table
  iasl -tc me176c-acpi/dsdt.dsl

  cd ${_srcname}

  # add upstream patch
  patch -p1 -i ../patch-${pkgver}
  chmod +x tools/objtool/sync-check.sh  # GNU patch doesn't support git-style file mode

  # security patches

  # add latest fixes from stable queue, if needed
  # http://git.kernel.org/?p=linux/kernel/git/stable/stable-queue.git

  # ASUS MeMO Pad 7 (ME176C/CX) patches
  echo ../*.patch | xargs -n1 patch -p1 -i

  cp -Tf ../config .config

  # Append option for custom DSDT table
  echo "CONFIG_ACPI_CUSTOM_DSDT_FILE=\"`realpath ../me176c-acpi/dsdt.hex`\"" >> .config

  # set extraversion to pkgrel
  sed -ri "s|^(EXTRAVERSION =).*|\1 -${pkgrel}|" Makefile

  # don't run depmod on 'make install'. We'll do this ourselves in packaging
  sed -i '2iexit 0' scripts/depmod.sh

  # get kernel version
  make prepare

  # load configuration
  # Configure the kernel. Replace the line below with one of your choice.
  #make menuconfig # CLI menu for configuration
  #make nconfig # new CLI menu for configuration
  #make xconfig # X-based configuration
  #make oldconfig # using old config from previous kernel version
  # ... or manually edit .config

  # rewrite configuration
  yes "" | make config >/dev/null
}

build() {
  cd ${_srcname}

  make ${MAKEFLAGS} LOCALVERSION= bzImage modules
}

_package() {
  pkgdesc="The ${pkgbase/linux/Linux} kernel and modules"
  [ "${pkgbase}" = "linux" ] && groups=('base')
  depends=('coreutils' 'linux-firmware' 'kmod' 'mkinitcpio>=0.7')
  optdepends=('crda: to set the correct wireless channels of your country')
  backup=("etc/mkinitcpio.d/${pkgbase}.preset")
  install=linux.install

  cd ${_srcname}

  # get kernel version
  _kernver="$(make LOCALVERSION= kernelrelease)"
  _basekernel=${_kernver%%-*}
  _basekernel=${_basekernel%.*}

  mkdir -p "${pkgdir}"/{boot,usr/lib/modules}
  make LOCALVERSION= INSTALL_MOD_PATH="${pkgdir}/usr" modules_install
  cp arch/x86/boot/bzImage "${pkgdir}/boot/vmlinuz-${pkgbase}"

  # make room for external modules
  local _extramodules="extramodules-${_basekernel}${_kernelname:--ARCH}"
  ln -s "../${_extramodules}" "${pkgdir}/usr/lib/modules/${_kernver}/extramodules"

  # add real version for building modules and running depmod from hook
  echo "${_kernver}" |
    install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modules/${_extramodules}/version"

  # remove build and source links
  rm "${pkgdir}"/usr/lib/modules/${_kernver}/{source,build}

  # now we call depmod...
  depmod -b "${pkgdir}/usr" -F System.map "${_kernver}"

  # add vmlinux
  install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/build" -m644 vmlinux

  # sed expression for following substitutions
  local _subst="
    s|%PKGBASE%|${pkgbase}|g
    s|%KERNVER%|${_kernver}|g
    s|%EXTRAMODULES%|${_extramodules}|g
  "

  # hack to allow specifying an initially nonexisting install file
  sed "${_subst}" "${startdir}/${install}" > "${startdir}/${install}.pkg"
  true && install=${install}.pkg

  # install mkinitcpio preset file
  sed "${_subst}" ../linux.preset |
    install -Dm644 /dev/stdin "${pkgdir}/etc/mkinitcpio.d/${pkgbase}.preset"

  # install pacman hooks
  sed "${_subst}" ../60-linux.hook |
    install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/60-${pkgbase}.hook"
  sed "${_subst}" ../90-linux.hook |
    install -Dm644 /dev/stdin "${pkgdir}/usr/share/libalpm/hooks/90-${pkgbase}.hook"
}

_package-headers() {
  pkgdesc="Header files and scripts for building modules for ${pkgbase/linux/Linux} kernel"

  cd ${_srcname}
  local _builddir="${pkgdir}/usr/lib/modules/${_kernver}/build"

  install -Dt "${_builddir}" -m644 Makefile .config Module.symvers
  install -Dt "${_builddir}/kernel" -m644 kernel/Makefile

  mkdir "${_builddir}/.tmp_versions"

  cp -t "${_builddir}" -a include scripts

  install -Dt "${_builddir}/arch/x86" -m644 arch/x86/Makefile
  install -Dt "${_builddir}/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

  cp -t "${_builddir}/arch/x86" -a arch/x86/include

  install -Dt "${_builddir}/drivers/md" -m644 drivers/md/*.h
  install -Dt "${_builddir}/net/mac80211" -m644 net/mac80211/*.h

  # http://bugs.archlinux.org/task/9912
  install -Dt "${_builddir}/drivers/media/dvb-core" -m644 drivers/media/dvb-core/*.h

  # http://bugs.archlinux.org/task/13146
  install -Dt "${_builddir}/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # http://bugs.archlinux.org/task/20402
  install -Dt "${_builddir}/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "${_builddir}/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "${_builddir}/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # add xfs and shmem for aufs building
  mkdir -p "${_builddir}"/{fs/xfs,mm}

  # copy in Kconfig files
  find . -name Kconfig\* -exec install -Dm644 {} "${_builddir}/{}" \;

  # add objtool for external module building and enabled VALIDATION_STACK option
  install -Dt "${_builddir}/tools/objtool" tools/objtool/objtool

  # remove unneeded architectures
  local _arch
  for _arch in "${_builddir}"/arch/*/; do
    [[ ${_arch} == */x86/ ]] && continue
    rm -r "${_arch}"
  done

  # remove files already in linux-docs package
  rm -r "${_builddir}/Documentation"

  # remove now broken symlinks
  find -L "${_builddir}" -type l -printf 'Removing %P\n' -delete

  # Fix permissions
  chmod -R u=rwX,go=rX "${_builddir}"

  # strip scripts directory
  local _binary _strip
  while read -rd '' _binary; do
    case "$(file -bi "${_binary}")" in
      *application/x-sharedlib*)  _strip="${STRIP_SHARED}"   ;; # Libraries (.so)
      *application/x-archive*)    _strip="${STRIP_STATIC}"   ;; # Libraries (.a)
      *application/x-executable*) _strip="${STRIP_BINARIES}" ;; # Binaries
      *) continue ;;
    esac
    /usr/bin/strip ${_strip} "${_binary}"
  done < <(find "${_builddir}/scripts" -type f -perm -u+w -print0 2>/dev/null)
}

_package-docs() {
  pkgdesc="Kernel hackers manual - HTML documentation that comes with the ${pkgbase/linux/Linux} kernel"

  cd ${_srcname}
  local _builddir="${pkgdir}/usr/lib/modules/${_kernver}/build"

  mkdir -p "${_builddir}"
  cp -t "${_builddir}" -a Documentation

  # Fix permissions
  chmod -R u=rwX,go=rX "${_builddir}"
}

pkgname=("${pkgbase}" "${pkgbase}-headers" "${pkgbase}-docs")
for _p in ${pkgname[@]}; do
  eval "package_${_p}() {
    $(declare -f "_package${_p#${pkgbase}}")
    _package${_p#${pkgbase}}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
